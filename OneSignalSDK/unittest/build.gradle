ext {
    buildVersions = [
            minSdkVersion: 17,
            versionCode: 1,
            versionName: '1.0'
    ]
    androidTestCoreVersion = '1.2.0'
    androidWorkTestVersion = '2.3.4'
    awaitilityVersion = '3.1.5'
    firebaseCoreVersion = '17.2.2'
    googlePlayServicesVersion = '17.0.0'
    jUnitVersion = '4.12'
    reflectionsVersion = '0.9.12'
    roboelectricVersion = '4.3.1'
    spek_version = '2.0.14'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Required as JUnit 5 doesn't support android modules out of the box.
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion rootProject.buildVersions.compileSdkVersion
    defaultConfig {
        applicationId 'com.onesignal.example'
        targetSdkVersion rootProject.buildVersions.targetSdkVersion
        minSdkVersion buildVersions.minSdkVersion
        versionCode buildVersions.versionCode
        versionName buildVersions.versionName
        testOptions.unitTests.includeAndroidResources = true
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // Add Kotlin source directory to all source sets
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }
    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
            jacocoOptions {
                // here goes all jacoco config, for example
                html.enabled = true
                xml.enabled = false
                csv.enabled = false
            }
        }
        unitTests.all {
            maxParallelForks 1
            maxHeapSize '2048m'
        }
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams false // Enable to have logging print
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    // Use local OneSignal SDK
    implementation(project(':onesignal'))

    // Required to shadow firebase analytics classes
    implementation "com.google.android.gms:play-services-location:$googlePlayServicesVersion"
    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"

    implementation "com.huawei.hms:push:$huaweiHMSPushVersion"
    implementation "com.huawei.hms:location:$huaweiHMSLocationVersion"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    testImplementation "junit:junit:$jUnitVersion"

    // spek
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    // spek requires kotlin-reflect, omit when already in classpath
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    testImplementation "androidx.test:core:$androidTestCoreVersion"
    testImplementation "androidx.work:work-testing:$androidWorkTestVersion"

    testImplementation "org.robolectric:robolectric:$roboelectricVersion"
    testImplementation "org.awaitility:awaitility:$awaitilityVersion"
    testImplementation "org.reflections:reflections:$reflectionsVersion"
}
